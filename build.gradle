buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.72'
    }
}

repositories {
    mavenCentral()
}

apply plugin: 'org.jetbrains.kotlin.multiplatform'

kotlin {
    macosX64 {
        sourceSets {
            macosX64Main {
                kotlin.srcDir 'src/tetrisMain/kotlin'
            }
        }

        binaries {
            executable {
                entryPoint 'sample.tetris.main'

                linkerOpts '-L/opt/local/lib', '-L/usr/local/lib', '-lSDL2'

                String distTaskName = linkTaskName.replaceFirst('link', 'dist')
                def distTask = tasks.register(distTaskName, Copy) {
                    from 'src/tetrisMain/resources'
                    into linkTask.outputFile.get().parentFile
                    exclude '*.rc'
                    dependsOn linkTask
                }

                tasks['assemble'].dependsOn(distTask)

                runTask.workingDir(project.provider { outputDirectory })
            }
        }

        compilations.main {
            cinterops {
                sdl {
                    includeDirs '/opt/local/include/SDL2', '/usr/local/include/SDL2'
                }
            }
           enableEndorsedLibs = true
        }
    }
}

